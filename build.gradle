buildscript {
    ext.kotlin_version = '1.6.10'
    ext.koin_version = '3.1.5'

    repositories {
        mavenCentral()

    }

}

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "nebula.deb" version "9.1.1"
}

group 'brewcontroller'
version '0.1.'+(System.env.BUILD_NUMBER ?: 1)



sourceCompatibility = 1.8

repositories {
    mavenCentral()

}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
//    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "org.danilopianini:khttp:1.1.3"
    implementation 'io.github.microutils:kotlin-logging:2.1.21'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'com.pi4j:pi4j-core:2.1.1'
    implementation 'com.pi4j:pi4j-gpio-extension:1.3'
    implementation 'com.pi4j:pi4j-device:1.3'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.0'
    testImplementation group: 'junit', name: 'junit', version: '4.11'

}

jar {
    manifest {
        attributes 'Main-Class' : 'uk.co.tertiarybrewery.brewcontroller.BrewControllerKt'
    }

}


task createDeb(type: Deb ) {
    packageName = "brewcontroller"
    version="${project.version}"
    type=BINARY
    arch="armhf"
    requires('default-jre-headless').or('java6-runtime-headless')

    requires('wiringpi')

    postInstall 'curl -sSL https://pi4j.com/install | sudo bash && update-rc.d brewcontroller defaults'
    preUninstall 'update-rc.d brewcontroller remove'
    into '/usr/local'
    from(jar.outputs.files) {
        rename { String fileName ->
            fileName.replace("-${project.version}", "")
        }
        fileMode 0500
        into 'lib'
    }
    from('src/main/resources') {
        rename { String fileName ->
            fileName.replace("application.properties", "brewcontroller.conf")
        }
        exclude("logging.properties")
        into '/etc'
    }

    from('src/main/resources/scripts') {
        fileMode 0755
        into '/etc/init.d'
    }
}