group 'brewcontroller'
version '0.1.'+(System.env.BUILD_NUMBER ?: 1)

buildscript {
    ext.kotlin_version = '1.2.40'
    ext.koin_version = '0.9.3'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("com.netflix.nebula:gradle-ospackage-plugin:4.4.0")
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'nebula.deb'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.23.1'
    compile "org.koin:koin-core:$koin_version"
    compile "khttp:khttp:0.1.0"
    compile 'io.github.microutils:kotlin-logging:1.5.4'
    compile 'org.slf4j:slf4j-simple:1.7.5'
    compile "com.pi4j:pi4j-core:1.1"
    compile "com.pi4j:pi4j-gpio-extension:1.1"
    compile "com.pi4j:pi4j-device:1.1"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
}

jar {
    manifest {
        attributes 'Main-Class' : 'uk.co.tertiarybrewery.brewcontroller.BrewControllerKt'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}


task createDeb(type: Deb ) {
    packageName = "brewcontroller"
    version="${project.version}"
    type=BINARY
    arch="armhf"
    requires('default-jre-headless').or('java6-runtime-headless')

    requires('wiringpi')

    postInstall 'curl -s get.pi4j.com | sudo bash && update-rc.d brewcontroller defaults'
    preUninstall 'update-rc.d brewcontroller remove'
    into '/usr/local'
    from(jar.outputs.files) {
        rename { String fileName ->
            fileName.replace("-${project.version}", "")
        }
        fileMode 0500
        into 'lib'
    }
    from('src/main/resources') {
        rename { String fileName ->
            fileName.replace("application.properties", "brewcontroller.conf")
        }
        exclude("logging.properties")
        into '/etc'
    }

    from('src/main/resources/scripts') {
        fileMode 0755
        into '/etc/init.d'
    }
}